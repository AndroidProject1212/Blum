apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.andreapivetta.blu"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 53
        versionName "5.1.1"

        resConfigs "en", "it"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {
            buildConfigField 'String', 'TWITTER_CONSUMER_KEY', ConsumerKey
            buildConfigField 'String', 'TWITTER_CONSUMER_SECRET', ConsumerSecret
            buildConfigField 'long', 'INTERVAL', '60'

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            buildConfigField 'String', 'TWITTER_CONSUMER_KEY', DebugConsumerKey
            buildConfigField 'String', 'TWITTER_CONSUMER_SECRET', DebugConsumerSecret
            buildConfigField 'long', 'INTERVAL', '20'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'TWITTER_CALLBACK', CallBackUrl
        it.buildConfigField 'String', 'LICENSES_URL', LicensesUrl
        it.buildConfigField 'String', 'TEST_TOKEN', TestToken
        it.buildConfigField 'String', 'TEST_SECRET', TestSecret
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'
        }
    }
}

dependencies {
    compile "com.android.support:support-fragment:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:customtabs:$SUPPORT_LIBRARY_VERSION"

    compile "io.reactivex.rxjava2:rxjava:$RX_JAVA_VERSION"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile "org.twitter4j:twitter4j-core:$TWITTER4J_VERSION"

    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.mlsdev.rximagepicker:library:2.0.0'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'org.jsoup:jsoup:1.10.2'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.uwetrottmann.photoview:library:1.2.4'
    compile 'com.evernote:android-job:1.1.9'
    compile 'com.github.armcha:AutoLinkTextView:0.3.0'
    compile fileTree(dir: 'libs', include: '*.jar') // compile 'com.schinizer:rxunfurl:0.2.0'
    compile project(path: ':loginView')

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    debugCompile 'com.uphyca:stetho_realm:2.0.0'

    releaseCompile "com.google.firebase:firebase-crash:$FIREBASE_VERSION"

    testCompile 'junit:junit:4.12'
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$KOTLIN_VERSION"
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN_VERSION"
    }
}

ext.versionCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'mips': 3, 'x86': 4, 'x86_64': 5]
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        output.versionCodeOverride = project.versionCodes.get(output.getFilter(
                com.android.build.OutputFile.ABI)) * 100 + android.defaultConfig.versionCode
    }
}

apply plugin: 'com.google.gms.google-services'